// TITLE " Magic Momentum with Magnetic Focusing (MMMF) Proton Storage Ring"
// No dipole magnetic field, Elctric focusing, Spin precession is Zero
// setopts, echo
// !------------  Physical costants -----------------------------
Ed0    := 1.875612762;  //              ! deuteron mass [GeV]
Ep0    := 0.938272029;  //              ! proton mass [GeV]
C      := 2.99792458e8; //              ! speed of light [m/sec]
twopie := 6.28318531;   // 
pie    := twopie/2;     // 
// pie := 3.1415926535897931159979634685441851615905761718750
// !
// !------------  Beam parameters   -----------------------------
BEAM  MASS=0.938272029, CHARGE=1, PC=0.700741; 

// !                      
// !------------  Machine parameters ---------------------------- 
P     := 0.700741;          //     include electric field effect
R     := 27.9544074;      //     curv radius of the BE sections [m]
Bv    := 0.0836169;        //      effective magnetic field [T]
Er    := 15000000;      //     radial electric field [V/m]
BETA  := P/Ep0/sqrt(1+P*P/(Ep0*Ep0)); 
GAMMA := 1/sqrt(1-BETA*BETA);         
BRHO  := 1.e9*P/C;                         
B_eff := BRHO/R;                     
//an    := 3.6592233718/R;
an    := 5.48883506/R;   
//Ggam  := -0.1429875554 * GAMMA;  // deuteron
Ggam  := 1.7928456 * GAMMA;    // proton 
// value BETA; value GAMMA; value BRHO; value B_eff; value Ggam
// value an;

// !
// !------------  BE magnet --------------------------------------
lenBE := 5.48883506;
ang   := lenBE/R;
B1S   := 0.0 ;                        //   [T/m2]
K2BS  := B1S/BRHO;
SBENBE: sbend, L= 5.48883506, angle= ang;  !, K2= K2BS  !, K1= 0.003035 

// !
// !------------  Quads.in BE section ----------------------------
BFQ     :=  4.08203176559;    // 3.99151851765;   //3.57147318827;
BDQ     := -3.85633185860; 
K1F     := BFQ/BRHO;  
K1D     := BDQ/BRHO; 
QUADFQ: quadrupole,  L=0.15,    K1=K1F;
QUADDQ: quadrupole,  L=0.15,    K1=K1D;
QUADFQ2: quadrupole,  L=0.075,  K1=K1F;

// !
// !------------  Drift in Bend magent  ----------------------------
L1:= 0.2;
DRIFDBE: drift, L= L1;        //    Drift between Bend and FQ(DQ)/SXF(SXD)

// !
// !------------  Quads in straight section  ------------------------
BFQS     := 4.41358859621;   // 8.46455088896;    //       [T/m]
K1FQS    := BFQS/BRHO; 
QUADFQS: quadrupole,  L=0.375,   K1=K1FQS;                
QUADDQS: quadrupole,  L=0.375,   K1=-K1FQS;
QUADFQS2: quadrupole,  L=0.1875, K1=K1FQS;

// !
// !------------  Sexts.in straight section  --------------------------
BSXS    := 0.0;
K2SXS   := BSXS/BRHO;
SEXTSXS: sextupole, L=0., K2=K2SXS;

// !
// !------------  RF cavity in straight section -----------------------
//RFCARF: rfcavity, VOLT = 1.0, HARMON = 5.0;
RFCARF: rfcavity;

// !
// !------------  Free Inter.in one straight section  ------------------
DRIFDSS: drift, L= 0.8;            // ! [m]
DRIFDSS1:drift, L= 0.3;            // ! 1.2 - 0.9

// !
// !------------  One straight section  --------------------------------
LSSRF: line = ( QUADFQS2, DRIFDSS, QUADDQS, DRIFDSS,     
              QUADFQS, DRIFDSS, QUADDQS, DRIFDSS, RFCARF, 
              QUADFQS, DRIFDSS, QUADDQS, DRIFDSS,        
              QUADFQS, DRIFDSS, QUADDQS, DRIFDSS, QUADFQS2 );

//BES1: line = ( QUADFQ2, SEXTSXF2, DRIFDBE, SBENBE, DRIFDBE,  
//               QUADDQ, SEXTSXD, DRIFDBE, SBENBE, DRIFDBE,    
//               QUADFQ, SEXTSXF, DRIFDBE, SBENBE, DRIFDBE,    
//               QUADDQ, SEXTSXD, DRIFDBE, SBENBE, DRIFDBE,    
//               QUADFQ, SEXTSXF, DRIFDBE, SBENBE, DRIFDBE,    
//               QUADDQ, SEXTSXD, DRIFDBE, SBENBE, DRIFDBE,    
//               QUADFQ, SEXTSXF, DRIFDBE, SBENBE, DRIFDBE,    
//               QUADDQ, SEXTSXD, DRIFDBE, SBENBE, DRIFDBE,    
//               SEXTSXF2, QUADFQ2 );

BES1: line = (QUADFQ2, DRIFDBE, SBENBE, DRIFDBE,
              QUADDQ, DRIFDBE, SBENBE, DRIFDBE, QUADFQ2 );

// !------------  EDM lattice  -------------------------------------------
mend: marker; 
//EDM:  line = (LSSRF, BES1, LSSRF, BES1, mend);
//EDM:  line = (BES1, BES1);
EDM:  line = (LSSRF, 8*BES1, LSSRF, 8*BES1, mend);
// !
// !
use, period=EDM;
twiss, sequence=EDM;

//select, flag=twiss_table, Range = #S/#E;
select, flag=twiss_table, Range = #S/#E, column=NAME,KEYWORD,L,ANGLE,K1L,K2L,S;
//select, flag=twiss_table, Range = #S/#E, column=NAME,KEYWORD,L,ANGLE,BETX,ALFX,BETY,ALFY,DX,DPX;

twiss, table=twiss_table, file = "edm.4.8.twiss";

//select, flag=twiss_table, column=s,betx,dx,dy,K1;
//twiss,  table=twiss_table, file = "edm.4.8.twiss";

plot, table=twiss_table vaxis1=betx,bety, vaxis2=dx,dy, haxis=s, colour=100, interpolate=true, title="EDM4.8", file="edm.4.8";

sxfwrite, file = "edm.4.8.sxf";

select,flag=SECTORMAP,clear;
twiss,SECTORMAP,range=$S/$E;

stop;
