!
! MadX lattice
! Electron Stretcher Ring ELSA
!
! loaded by elsa.madx, which contains optics and beam definitions
!

tiny:=0.0000000001;

!********************
! Magnets
!********************

! Dipole
L2_EFF:=2.86679664;             // eff. length
L2_MET:=2.84000;                // iron length (not used!)
WINKEL:=+PI/12;                 // bending angle
B_FACT:=PI/24/sin(PI/24);       // length correction factor (curved length along orbit)
SDPSTR:= -0.085;                // sextupole strength of dipoles because of magnetized chamber ribs

! Quadrupole
L4_EFF:=0.4997;			// eff. length
L4_MET:=0.4500;			// iron length (not used!)

! Sextupole
L6_EFF:=0.287;			// eff. length
L6_MET:=0.250;			// iron length (not used!)


! Dipole
DIP:SBEND, L:=L2_EFF*B_FACT, ANGLE:=WINKEL, E1:=WINKEL/2, E2:=WINKEL/2, K2:=SDPSTR; //optional: K1:= 7E-04

LQSTR:=tiny;

! Quadrupole
QDF:quadrupole, L:=L4_EFF, K1:=QFOK;         // focussing (F) quadrupole
QDD:quadrupole, L:=L4_EFF, K1:=QDEF;         // defocussing (D) quadrupole
LQD16:quadrupole, L:=0.45, K1:=LQSTR;        // air quadrupole
LQD32:quadrupole, L:=0.45, K1:=LQSTR;
TJQD32:quadrupole, L=0.5515, K1= tiny; ! 0.0;        // Tune-Jump-Quadrupole
TJQD16:quadrupole, L=0.5515, K1= tiny; ! 0.0;
SQD1:quadrupole, L=0.45, K1= tiny;     ! 0.0, TILT=pi/4;  // Skew-Quadrupole ! TILT sign not checked
SQD32:quadrupole, L=0.45, K1= tiny;    ! 0.0, TILT=pi/4;

! Sextupole
SXTF:sextupole, L:=L6_EFF, K2:=SFOK;         // F-Sextupole
SXTD:sextupole, L:=L6_EFF, K2:=SDEF;         // D-Sextupole
SXTEX:sextupole, L:=L6_EFF, K2:=0.000000001;      // sextupoles for extraction

! Corrector
KODIV:  VKICKER, L=0.1;                      // old vertikal corrector magnet
NKODIV: VKICKER, L=0.13;                     // new vertikal corrector magnet

! Septum (horizontal collimator, vertical arbitrary large value given)
SEPTUM1:RCOLLIMATOR, L=0.65, APERTYPE=RECTANGLE, APERTURE={0.02,1};   // Vorseptum
SEPTUM2:RCOLLIMATOR, L=0.9, APERTYPE=RECTANGLE, APERTURE={0.0165,1};  // Hauptseptum


!********************
! Other Elements
!********************

! PETRA-Cavity
RFC1:RFCAVITY, L:=1.6, VOLT:=PVOLT, SHUNT:=11.25, HARMON:=274, LAG:=0.5, APERTYPE=circle, APERTURE=0.06;

! Monitor
NMON:   MONITOR, L=0.025;              // new monitors (ESRF-Buttons)
RMON:   MONITOR, L= 0.0;                // old monitors (DESY-Buttons)

! Ion Clearing Electrode
IONCL:	MARKER;

! injection kicker
KICKER29:HKICKER, L:=0.73;
KICKER2:HKICKER, L:=0.73;
KICKER3:HKICKER, L:=0.73;

! injection septa
ISEP1:HKICKER, L=0.90;
ISEP2:HKICKER, L=0.60;


!********************
! ELSA Lattice
!********************
ELSA:SEQUENCE, REFER=CENTRE, l=164.4;
  MY_START:MARKER, AT=0.0;
  QD1:QDD, AT=0.2840;
  SQ1:SQD1, AT=2.85275;
  LQ1:LQD32, AT=4.3165;
  SX1:SXTEX, AT=4.8915;
  QF2:QDF, AT=5.4215;
  IJK2:KICKER2, AT=6.2820;
  M2:DIP, AT=8.5702;
  QD3:QDD, AT=10.5590;
  SD3:SXTD, AT=11.0890;
  IJK3:KICKER3, AT= 14.8410;
  QF4:QDF, AT=15.6965;
  SF4:SXTF, AT=16.2265;
  M4:DIP, AT=18.8452;
  QD5:QDD, AT=20.8340;
  M5:DIP, AT=23.9827;
  QF6:QDF, AT=25.9715;
  MSE6:SEPTUM1, AT=26.752;
  M6:DIP, AT=29.1202;
  QD7:QDD, AT=31.1090;
  MSE7:SEPTUM2, AT=32.0275;
  M7:DIP, AT=34.2577;
  QF8:QDF, AT=36.2465;
  M8:DIP, AT=39.3952;
  QD9:QDD, AT=41.3840;
  M9:DIP, AT=43.3727;
  QF10:QDF, AT=46.5215;
  M10:DIP, AT=48.5102;
  SD10:SXTD, AT=51.1290;
  QD11:QDD, AT=51.6590;
  M11:DIP, AT=53.6477;
  SF11:SXTF, AT=56.2665;
  QF12:QDF, AT=56.7965;
  M12:DIP, AT=58.7852;
  QD13:QDD, AT=61.9340;
  M13:DIP, AT=63.9227;
  QF14:QDF, AT=67.0715;
  QD15:QDD, AT=72.2090;
  M15:DIP, AT=74.1977;
  QF16:QDF, AT=77.3465;
  SX16:SXTEX, AT=77.8765;
  LQ16:LQD16, AT=78.4515;
  PETRA1:RFC1, AT=79.7840;
  TJQD16:TJQD16, AT=81.7970; 
  QD17:QDD, AT=82.4840;
  PETRA2:RFC1, AT=83.9840;
  LQ17:LQD16, AT=86.5165;
  SX17:SXTEX, AT=87.0915;
  QF18:QDF, AT=87.6215;
  M18:DIP, AT=90.7702;
  QD19:QDD, AT=92.7590;
  SD19:SXTD, AT=93.2890;
  SF19:SXTF, AT=97.3665;
  QF20:QDF, AT=97.8965;
  M20:DIP, AT=101.0452;
  QD21:QDD, AT=103.0340;
  M21:DIP, AT=106.1825;
  QF22:QDF, AT=108.1715;
  MSE22:SEPTUM1, AT=108.584+0.325;
  M22:DIP, AT=111.3205;
  QD23:QDD, AT=113.3090;
  MSE23:SEPTUM2, AT=113.769+0.45;
  M23:DIP, AT=116.4575;
  QF24:QDF, AT=118.4465;
  M24:DIP, AT=121.5955;
  QD25:QDD, AT=123.5840;
  M25:DIP, AT=125.5725;
  QF26:QDF, AT=128.7215;
  M26:DIP, AT=130.7105;
  SD26:SXTD, AT=133.3285;
  QD27:QDD, AT=133.8590;
  M27:DIP, AT=135.8475;
  SF27:SXTF, AT=138.4665;
  QF28:QDF, AT=138.9965;
  M28:DIP, AT=140.9855;
  QD29:QDD, AT=144.1340;
  M29:DIP, AT=146.1225;
  IJK29:KICKER29, AT=148.4110;
  QF30:QDF, AT=149.2715;
  MSI30: ISEP1, AT=153.4350;
  QD31:QDD, AT=154.4090;
  M31:DIP, AT=156.3975;
  MSI31: ISEP2, AT=158.7030;
  QF32:QDF, AT=159.5465;
  SX32:SXTEX, AT=160.0765;
  LQ32:LQD32, AT=160.6519;
  SQ32:SQD32, AT=162.11525;
  TJQ32: TJQD32, AT=163.9980;
  ENDM:MARKER, AT=164.4;
ENDSEQUENCE;


!********************
! Monitors (MON: DESY-Buttons; NMON: ESRF-Buttons)
!********************
SEQEDIT, SEQUENCE=ELSA;
  INSTALL, ELEMENT=FMON,  CLASS=RMON,  AT= 94.280, FROM=MY_START;
  INSTALL, ELEMENT=BPM01, CLASS=NMON,  AT=0.55,  FROM=MY_START; 
  INSTALL, ELEMENT=BPM02, CLASS=NMON,  AT=5.69,  FROM=MY_START; 
  INSTALL, ELEMENT=BPM03, CLASS=NMON,  AT=10.83, FROM=MY_START; 
  INSTALL, ELEMENT=BPM04, CLASS=NMON,  AT=15.97, FROM=MY_START; 
  INSTALL, ELEMENT=BPM05, CLASS=NMON,  AT=21.10, FROM=MY_START; 
  INSTALL, ELEMENT=BPM06, CLASS=NMON,  AT=26.24, FROM=MY_START; 
  INSTALL, ELEMENT=BPM07, CLASS=NMON,  AT=31.38, FROM=MY_START; 
  INSTALL, ELEMENT=BPM08, CLASS=NMON,  AT=36.52, FROM=MY_START; 
  INSTALL, ELEMENT=BPM09, CLASS=NMON,  AT=41.65, FROM=MY_START; 
  INSTALL, ELEMENT=BPM10, CLASS=NMON,  AT=46.79, FROM=MY_START; 
  INSTALL, ELEMENT=BPM11, CLASS=NMON,  AT=51.39, FROM=MY_START; 
  INSTALL, ELEMENT=BPM12, CLASS=NMON,  AT=56.53, FROM=MY_START; 
  INSTALL, ELEMENT=BPM13, CLASS=NMON,  AT=62.20, FROM=MY_START; 
  INSTALL, ELEMENT=BPM14, CLASS=NMON,  AT=67.34, FROM=MY_START; 
  INSTALL, ELEMENT=BPM15, CLASS=NMON,  AT=72.48, FROM=MY_START; 
  INSTALL, ELEMENT=BPM16, CLASS=NMON,  AT=77.62, FROM=MY_START; 
  INSTALL, ELEMENT=BPM17, CLASS=NMON,  AT=82.21, FROM=MY_START; 
  INSTALL, ELEMENT=BPM18, CLASS=NMON,  AT=87.89, FROM=MY_START; 
  INSTALL, ELEMENT=BPM19, CLASS=NMON,  AT=93.03, FROM=MY_START; 
  INSTALL, ELEMENT=BPM20, CLASS=NMON,  AT=97.63, FROM=MY_START; 
  INSTALL, ELEMENT=BPM21, CLASS=NMON,  AT=103.30, FROM=MY_START; 
  INSTALL, ELEMENT=BPM22, CLASS=NMON,  AT=108.44, FROM=MY_START; 
  INSTALL, ELEMENT=BPM23, CLASS=NMON,  AT=113.58, FROM=MY_START; 
  INSTALL, ELEMENT=BPM24, CLASS=NMON,  AT=118.72, FROM=MY_START; 
  INSTALL, ELEMENT=BPM25, CLASS=NMON,  AT=123.31, FROM=MY_START; 
  INSTALL, ELEMENT=BPM26, CLASS=NMON,  AT=128.99, FROM=MY_START; 
  INSTALL, ELEMENT=BPM27, CLASS=NMON,  AT=133.59, FROM=MY_START; 
  INSTALL, ELEMENT=BPM28, CLASS=NMON,  AT=138.73, FROM=MY_START; 
  INSTALL, ELEMENT=BPM29, CLASS=NMON,  AT=144.40, FROM=MY_START; 
  INSTALL, ELEMENT=BPM30, CLASS=NMON,  AT=149.54, FROM=MY_START; 
  INSTALL, ELEMENT=BPM31, CLASS=NMON,  AT=154.13, FROM=MY_START; 
  INSTALL, ELEMENT=BPM32, CLASS=NMON,  AT=159.82, FROM=MY_START; 
ENDEDIT;


!********************
! Vertical Correctors (KODIV: old magnets NKODIV: new magnets)
!********************
CORR01: KODIV, KICK=0.0;
CORR02: NKODIV, KICK=0.0;
CORR03: NKODIV, KICK=0.0;
CORR04: NKODIV, KICK=0.0;
CORR05: NKODIV, KICK=0.0;
CORR06: NKODIV, KICK=0.0;
CORR08: NKODIV, KICK=0.0;
CORR09: NKODIV, KICK=0.0;
CORR10: NKODIV, KICK=0.0;
CORR11: NKODIV, KICK=0.0;
CORR12: NKODIV, KICK=0.0;
CORR13: NKODIV, KICK=0.0;
CORR14: NKODIV, KICK=0.0;
CORR15: NKODIV, KICK=0.0;
CORR17: NKODIV, KICK=0.0;
CORR18: NKODIV, KICK=0.0;
CORR19: NKODIV, KICK=0.0;
CORR20: NKODIV, KICK=0.0;
CORR21: NKODIV, KICK=0.0;
CORR22: NKODIV, KICK=0.0;
CORR24: NKODIV, KICK=0.0;
CORR25: NKODIV, KICK=0.0;
CORR26: NKODIV, KICK=0.0;
CORR27: NKODIV, KICK=0.0;
CORR28: NKODIV, KICK=0.0;
CORR29: NKODIV, KICK=0.0;
CORR30: NKODIV, KICK=0.0;
CORR31: NKODIV, KICK=0.0;
CORR32: KODIV, KICK=0.0;

! Positions from measurements of distance to neighbouring elements.
! Every distance measured form element end to element begin, then
! half length of both elements was added to the distance to get
! distance of reference positions (CENTER).
! Comments give measurement errors.
! Measurements Jens-Peter Thiry, December 2015
SEQEDIT, SEQUENCE=ELSA;
  INSTALL, ELEMENT=KV01, CLASS=CORR01, AT=0.500, FROM=QD1; ! ± 0.004
  INSTALL, ELEMENT=KV02, CLASS=CORR02, AT=1.350, FROM=QF2; ! ± 0.004
  INSTALL, ELEMENT=KV03, CLASS=CORR03, AT=0.841, FROM=QD3; ! ± 0.004
  INSTALL, ELEMENT=KV04, CLASS=CORR04, AT=0.938, FROM=QF4; ! ± 0.004
  INSTALL, ELEMENT=KV05, CLASS=CORR05, AT=0.498, FROM=QD5; ! ± 0.004
  INSTALL, ELEMENT=KV06, CLASS=CORR06, AT=1.328, FROM=QF6; ! ± 0.004
  INSTALL, ELEMENT=KV08, CLASS=CORR08, AT=1.322, FROM=QF8; ! ± 0.004
  INSTALL, ELEMENT=KV09, CLASS=CORR09, AT=-1.299, FROM=QF10; ! ± 0.004
  INSTALL, ELEMENT=KV10, CLASS=CORR10, AT=-0.950, FROM=QD11; ! ± 0.004
  INSTALL, ELEMENT=KV11, CLASS=CORR11, AT=-0.936, FROM=QF12; ! ± 0.004
  INSTALL, ELEMENT=KV12, CLASS=CORR12, AT=-0.490, FROM=QD13; ! ± 0.004
  INSTALL, ELEMENT=KV13, CLASS=CORR13, AT=-0.920, FROM=QF14; ! ± 0.004
  INSTALL, ELEMENT=KV14, CLASS=CORR14, AT=-1.165, FROM=QD15; ! ± 0.004
  INSTALL, ELEMENT=KV15, CLASS=CORR15, AT=-0.498, FROM=QF16; ! ± 0.004
  INSTALL, ELEMENT=KV17, CLASS=CORR17, AT=1.176, FROM=PETRA2; ! ± 0.025
  INSTALL, ELEMENT=KV18, CLASS=CORR18, AT=1.308, FROM=QF18; ! ± 0.004
  INSTALL, ELEMENT=KV19, CLASS=CORR19, AT=0.949, FROM=QD19; ! ± 0.004
  INSTALL, ELEMENT=KV20, CLASS=CORR20, AT=1.321, FROM=QF20; ! ± 0.004
  INSTALL, ELEMENT=KV21, CLASS=CORR21, AT=0.520, FROM=QD21; ! ± 0.004
  INSTALL, ELEMENT=KV22, CLASS=CORR22, AT=1.293, FROM=QF22; ! ± 0.004
  INSTALL, ELEMENT=KV24, CLASS=CORR24, AT=1.237, FROM=QF24; ! ± 0.004
  INSTALL, ELEMENT=KV25, CLASS=CORR25, AT=-0.948, FROM=QF26; ! ± 0.004
  INSTALL, ELEMENT=KV26, CLASS=CORR26, AT=-0.917, FROM=QD27; ! ± 0.004
  INSTALL, ELEMENT=KV27, CLASS=CORR27, AT=-0.944, FROM=QF28; ! ± 0.004
  INSTALL, ELEMENT=KV28, CLASS=CORR28, AT=-0.487, FROM=QD29; ! ± 0.004
  INSTALL, ELEMENT=KV29, CLASS=CORR29, AT=-1.336, FROM=QF30; ! ± 0.004
  INSTALL, ELEMENT=KV30, CLASS=CORR30, AT=-0.763, FROM=MSI30; ! ± 0.025
  INSTALL, ELEMENT=KV31, CLASS=CORR31, AT=-1.370, FROM=QF32; ! ± 0.004
  INSTALL, ELEMENT=KV32, CLASS=CORR32, AT=1.876, FROM=QF32; ! ± 0.004
ENDEDIT;


!********************
! Ion Clearing Electrodes
!********************
! Dennis Sauerland, December 2015
SEQEDIT, SEQUENCE=ELSA;
  INSTALL, ELEMENT=IC01, CLASS=IONCL,  AT=0.013,  FROM=MY_START;
  INSTALL, ELEMENT=IC02, CLASS=IONCL,  AT=5.1175,  FROM=MY_START;
  INSTALL, ELEMENT=IC03, CLASS=IONCL,  AT=10.288,  FROM=MY_START;
  INSTALL, ELEMENT=IC04, CLASS=IONCL,  AT=15.4255,  FROM=MY_START;
  INSTALL, ELEMENT=IC05, CLASS=IONCL,  AT=20.563,  FROM=MY_START;
  INSTALL, ELEMENT=IC06, CLASS=IONCL,  AT=25.7005,  FROM=MY_START;
  INSTALL, ELEMENT=ICMSE6, CLASS=IONCL,  AT=27.216,  FROM=MY_START;
  INSTALL, ELEMENT=IC07, CLASS=IONCL,  AT=30.838,  FROM=MY_START;
  INSTALL, ELEMENT=IC08, CLASS=IONCL,  AT=35.9755,  FROM=MY_START;
  INSTALL, ELEMENT=IC10, CLASS=IONCL,  AT=46.2505,  FROM=MY_START;
  INSTALL, ELEMENT=IC11, CLASS=IONCL,  AT=51.93,  FROM=MY_START;
  INSTALL, ELEMENT=IC12, CLASS=IONCL,  AT=57.0675,  FROM=MY_START;
  INSTALL, ELEMENT=IC13, CLASS=IONCL,  AT=61.663,  FROM=MY_START;
  INSTALL, ELEMENT=IC14, CLASS=IONCL,  AT=66.8005,  FROM=MY_START;
  INSTALL, ELEMENT=IC15, CLASS=IONCL,  AT=71.938,  FROM=MY_START;
  INSTALL, ELEMENT=IC16, CLASS=IONCL,  AT=77.0755,  FROM=MY_START;
  INSTALL, ELEMENT=IC17, CLASS=IONCL,  AT=82.755,  FROM=MY_START;
  INSTALL, ELEMENT=IC18, CLASS=IONCL,  AT=87.3175,  FROM=MY_START;
  INSTALL, ELEMENT=IC19, CLASS=IONCL,  AT=92.488,  FROM=MY_START;
  INSTALL, ELEMENT=IC20, CLASS=IONCL,  AT=98.1675,  FROM=MY_START;
  INSTALL, ELEMENT=IC21, CLASS=IONCL,  AT=102.763,  FROM=MY_START;
  INSTALL, ELEMENT=IC22, CLASS=IONCL,  AT=107.9005,  FROM=MY_START;
  INSTALL, ELEMENT=ICMSE22, CLASS=IONCL,  AT=109.399,  FROM=MY_START;
  INSTALL, ELEMENT=IC23, CLASS=IONCL,  AT=113.038,  FROM=MY_START;
  INSTALL, ELEMENT=IC24, CLASS=IONCL,  AT=118.1755,  FROM=MY_START;
  INSTALL, ELEMENT=IC25, CLASS=IONCL,  AT=123.855,  FROM=MY_START;
  INSTALL, ELEMENT=IC26, CLASS=IONCL,  AT=128.4505,  FROM=MY_START;
  INSTALL, ELEMENT=IC27, CLASS=IONCL,  AT=134.13,  FROM=MY_START;
  INSTALL, ELEMENT=IC28, CLASS=IONCL,  AT=139.2675,  FROM=MY_START;
  INSTALL, ELEMENT=IC29, CLASS=IONCL,  AT=143.863,  FROM=MY_START;
  INSTALL, ELEMENT=IC30, CLASS=IONCL,  AT=149.0005,  FROM=MY_START;
  INSTALL, ELEMENT=IC31, CLASS=IONCL,  AT=154.680,  FROM=MY_START;
  INSTALL, ELEMENT=IC32, CLASS=IONCL,  AT=159.2775,  FROM=MY_START;

ENDEDIT;
