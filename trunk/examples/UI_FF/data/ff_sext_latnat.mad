!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
! ******* This is an SNS lattice including chromatic sextupoles   *******
! ******* RF cavities, and markers for collimators and  hard-edge *******
! ******* quadrupole fringe-fields				  *******
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!
!!!!!!!!!!!!!!!!!!!!!
!
! Parameters
!
!!!!!!!!!!!!!!!!!!!!!
!
! Magnetic rigidity
!
  Brho := 5.6573735 ! 1.0 GeV (for 1.3 GeV: factor 1.1981566)
!  Brho := 6.7777   ! 1.3 GeV
!
! Tunes and phase advance 
!
  QH:=6.3
  QV:=5.8
  MUH:=QH/4.0
  MUV:=QV/4.0
!
! Matching value for beta functions
!
  BEXD := 2.428
  BEYD := 13.047

!!!!!!!!!!!!!!!
!
! drifts
!
!!!!!!!!!!!!!!!

 OARC    : DRIFT, L = 1
 O1      : DRIFT, L = 6.85
 O11     : DRIFT, L = O1[L]/4
 O2      : DRIFT, L = 0.4
 O3      : DRIFT, L = 6.25
 O31      : DRIFT, L = O3[L]/5
 OZ : DRIFT, L = 0.0
 OQ1 : DRIFT, L = 0.0
 OQ2 : DRIFT, L = 0.0
 OQ3 : DRIFT, L = 0.0

!!!!!!!!!!!!!!
! Bends
!!!!!!!!!!!!!!
!
! angles
!
  ANG:= 2*PI/32 
  EE := ANG/2
!
! length
!
  lbnd := 1.5
!!!!!!!!!!!!!!!
!
! Dipoles
!
!!!!!!!!!!!!!!!

  BL:  Sbend, L=lbnd/2, Angle=EE, E1=0., E2=0.
  BR:  Sbend, L=lbnd/2, Angle=EE, E1=0., E2=0.
  BND: Sbend, L=lbnd, Angle=ANG, E1=0.0, E2=0.0

!!!!!!!!!!!!!!!
!
! Quadrupoles
!
!!!!!!!!!!!!!!!
!
! Arc Quad strengths for achromat in both planes
!
   KF:= 4.66011/1.1981566
   KD:=-4.94176/1.1981566
!
! "Matching" quad strength
!
   KMAT := -3.41118/1.1981566   
!
!  Doublet strengths
! 
   KS2 :=  4.29830/1.1981566
   KS3 := -4.58404/1.1981566
!
! Strengths for 1.3 Gev
!
!  KF:= 4.66011
!  KD:=-4.94176
!  KMAT := -3.41118
!  KS2 :=  4.29830
!  KS3 := -4.58404
!
!
!  old values
!
!  KF:= 4.65962
!  KD:=-4.94124
!  KMAT := -3.405
!  KS2 :=  4.298150
!  KS3 := -4.586139
!
! lengths
!
  lq := 0.5
  lq1 := 0.25/2
  lq2 := 0.7
  lq3 := 0.55
!
! Half Quadrupoles
!
 QDH     : QUADRUPOLE, L = lq/2, K1 = KD/Brho     ! focusing arc quad   (21Q40)
 QFH     : QUADRUPOLE, L = lq/2, K1 = KF/Brho     ! defocusing arc quad (21Q40)
 QFBH    : QUADRUPOLE, L = lq/2, K1 = KF/Brho     ! large focusing arc quad   (26Q38)
 QDMH    : QUADRUPOLE, L = lq/2, K1 = KMAT/Brho   ! "matching" quad (21Q40)
 QFLH    : QUADRUPOLE, L = lq2/2,K1 = KS2/Brho    ! long doublet quad (30Q65)
 QDCH    : QUADRUPOLE, L = lq3/2, K1 = KS3/Brho   ! short doublet quad (30Q45)
!
!
! Quadrupole fringes markers
!
  QDH1  :  MARKER
  QDH2  :  MARKER
  QFH1  :  MARKER
  QFH2  :  MARKER
  QFBH1 :  MARKER 
  QFBH2 :  MARKER 
  QDMH1 :  MARKER 
  QDMH2 :  MARKER 
  QFLH1 :  MARKER 
  QFLH2 :  MARKER 
  QDCH1 :  MARKER 
  QDCH2 :  MARKER 

!
!
! Quadrupole lines
!
!
 QD      :   LINE = (QDH1,QDH,QDH,QDH2)
 QF      :   LINE = (QFH1,QFH,QFH,QFH2)
 QFB     :   LINE = (QFBH1,QFBH,QFBH,QFBH2)
 QDM     :   LINE = (QDMH1,QDMH,QDMH,QDMH2)
 QFL     :   LINE = (QFLH1,QFLH,QFLH,QFLH2)
 QDC     :   LINE = (QDCH1,QDCH,QDCH,QDCH2)
!
!
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
! CHROMATICITY SEXTUPOLES
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!
PS70:=0.70
S30:  DRIFT, L=0.30
LSEX:=0.15
!
! strengths for natural chromaticity
!
VS1D:=0.0
VS2F:=0.0
VS3D:=0.0
VS4F:=0.0
!
! strengths for chromaticity to 0
!
!VS1D:=-2.891275
!VS2F:=2.563548
!VS3D:=-4.449269
!VS4F:=3.354977
!
! strengths for chromaticity to -2
!
!VS1D:=-2.075591E+00
!VS2F:=1.792185E+00
!VS3D:=-3.057019E+00
!VS4F:=2.534535E+00
!
!
!
S1D:   SEXTUPOLE, L=LSEX, K2=VS1D
S2F:   SEXTUPOLE, L=LSEX, K2=VS2F
S3D:   SEXTUPOLE, L=LSEX, K2=VS3D
S4F:   SEXTUPOLE, L=LSEX, K2=VS4F
!
LDAS:  DRIFT, L=PS70-LSEX
SSX1D: LINE = (S30,S1D,LDAS)
SSX2F: LINE = (S30,S2F,LDAS)
SSX3D: LINE = (LDAS,S3D,S30)
SSX4F: LINE = (LDAS,S4F,S30)
SSX5D: LINE = (LDAS,S1D,S30)


!!!!!!!!!!!!!!!!!!!!!!!
!
! RF Cavities
!
!!!!!!!!!!!!!!!!!!!!!!!
LREF:=1.7
ORF : DRIFT, L = O31[L] - LREF/2.5
RF1 : RFCAVITY, L = LREF, HARMON = 1, VOLT =  0.0
RF2 : RFCAVITY, L = LREF, HARMON = 2, VOLT = -0.0
RFL1: LINE = (ORF,ORF,RF1,ORF,ORF,RF1,ORF)
RFL2: LINE = (ORF,RF1,ORF,ORF,RF2,ORF,ORF)

!!!!!!!!!!!!!!!!!!!!!!!
!
! Collimators
!
!!!!!!!!!!!!!!!!!!!!!!!
MCOL1: MARKER
MCOL2: MARKER

!!!!!!!!!!!!
!
! SNS lines (Carefull the UAL parser does not support * or - in lines)
!
!!!!!!!!!!!!
!
!
  SC     : line = (QDM,O11,O11,O11,O11,QFL,O2,QDC,O31,O31,O31,O31,O31)
  SCM    : line = (O31,O31,O31,O31,O31,QDC,O2,QFL,O11,O11,O11,O11,QDM)
  SCOL   : line = (QDM,O11,O11,MCOL1,O11,O11,QFL,O2,QDC,O31,O31,MCOL2,&
                   O31,O31,O31)
  SCOLM  : line = (O31,O31,O31,O31,O31,QDC,O2,QFL,O11,O11,MCOL1,&
                   O11,O11,QDM)
  SCRF   : line = (QDM,O11,O11,O11,O11,QFL,O2,QDC,RFL1)
  SCRFM  : line = (RFL2,QDC,O2,QFL,O11,O11,O11,O11,QDM)
  ACF    : line = (OARC,BND,OARC,QF)
  ACFM   : line = (OARC,BND,OARC,QD)
  ACS1   : line = (SSX1D,BND,OARC,QFB)
  ACS2   : line = (SSX2F,BND,SSX3D,QD)
  ACS3   : line = (OARC,BND,SSX4F,QFB)
  ACS4   : line = (OARC,BND,SSX5D,QD)
  ACFL   : line = (OARC,BND,OARC)
!
! arc line
!
  ARC    : line = (ACF,ACFM,ACS1,ACS2,ACS3,ACS4,ACF,ACFL)
!
! insertion lines
!
  INSERT  : line = (SC,OZ,SCM)
  INSERTC : line = (SCOL,OZ,SCOLM)
  INSERTR : line = (SCRF,OZ,SCRFM)
!
! superperiod
!
  SP :      line = (INSERT,ARC)
  SPC:      line = (INSERTC,ARC)
  SPR:      line = (INSERTR,ARC)
!
! ring
!
!  RING  : line = (SP,SPC,SP,SPR)

RING  : line = (SP,SP,SP,SP)


